#### 3.13 **Compiling** finally 

(This section assumes a compiler generates class files with version number 50.0 or below, so that the *jsr* instruction may be used. See also ยง4.10.2.5.) 

Compilation of a try-finally statement is similar to that of try-catch. Prior to transferring control outside the try statement, whether that transfer is normal or abrupt, because an exception has been thrown, the finally clause must first be executed. For this simple example: 

```java
void tryFinally() {
  try {
	  tryItOut();
  } finally {
 	 	wrapItUp();
  }
}
```

the compiled code is: 

```java
Method void tryFinally()
0 aload_0									// Beginning of try block
1 invokevirtual #6				// Method Example.tryItOut()V
4 jsr 14									// Call finally block
7 return									// End of try block
8 astore_1								// Beginning of handler for any throw
9 jsr 14									// Call finally block
12 aload_1								// Push thrown value
13 athrow									// ...and rethrow value to the invoker
14 astore_2								// Beginning of finally block
15 aload_0								// Push this
16 invokevirtual #5				// Method Example.wrapItUp()V
19 ret 2									// Return from finally block
  
Exception table:	
From		To		Target		Type
0       4       8       any
```

There are four ways for control to pass outside of the try statement: by falling through the bottom of that block, by returning, by executing a break or continue statement, or by raising an exception. If tryItOut returns without raising an exception, control is transferred to the finally block using a *jsr* instruction. The *jsr 14* instruction at index *4* makes a "subroutine call" to the code for the finally block at index *14* (the finally block is compiled as an embedded subroutine). When the finally block completes, the *ret 2* instruction returns control to the instruction following the *jsr* instruction at index *4*. 

In more detail, the subroutine call works as follows: The *jsr* instruction pushes the address of the following instruction (*return* at index *7*) onto the operand stack before jumping. The *astore_2* instruction that is the jump target stores the address on the operand stack into local variable *2*. The code for the finally block (in this case the *aload_0* and *invokevirtual* instructions) is run. Assuming execution of that code completes normally, the *ret* instruction retrieves the address from local variable *2* and resumes execution at that address. The *return* instruction is executed, and tryFinally returns normally. 

A try statement with a finally clause is compiled to have a special exception handler, one that can handle any exception thrown within the try statement. If tryItOut throws an exception, the exception table for tryFinally is searched for an appropriate exception handler. The special handler is found, causing execution to continue at index *8*. The *astore_1* instruction at index *8* stores the thrown value into local variable *1*. The following *jsr* instruction does a subroutine call to the code for the finally block. Assuming that code returns normally, the *aload_1* instruction at index *12* pushes the thrown value back onto the operand stack, and the following *athrow* instruction rethrows the value. 

Compiling a try statement with both a catch clause and a finally clause is more complex: 

```java
void tryCatchFinally() {
  try {
  	tryItOut();
  } catch (TestExc e) {
  	handleExc(e);
  } finally {
  	wrapItUp();
  }
}
```

becomes: 

```java
Method void tryCatchFinally()
0 aload_0								// Beginning of try block
1 invokevirtual #4			// Method Example.tryItOut()V
4 goto 16								// Jump to finally block
7 astore_3							// Beginning of handler for TestExc;
												// Store thrown value in local var 3
8 aload_0								// Push this
9 aload_3								// Push thrown value
10 invokevirtual #6			// Invoke handler method:
												// Example.handleExc(LTestExc;)V
13 goto 16							// This goto is unnecessary, but was
												// generated by javac in JDK 1.0.2
16 jsr 26 							// Call finally block
19 return 							// Return after handling TestExc
20 astore_1							// Beginning of handler for exceptions
												// other than TestExc, or exceptions
												// thrown while handling TestExc
21 jsr 26								// Call finally block
24 aload_1							// Push thrown value...
25 athrow								// ...and rethrow value
26 astore_2							// Beginning of finally
27 aload_0							// Push this
28 invokevirtual #5 		// Method Example.wrapItUp()V
31 ret 2								// Return from finally block

Exception table:	
From		To		Target		Type
0				4 		7					Class TestExc
0 			16 		20				any
```

If the try statement completes normally, the *goto* instruction at index *4* jumps to the subroutine call for the finally block at index *16*. The finally block at index *26* is executed, control returns to the *return* instruction at index *19*, and tryCatchFinally returns normally. 

If tryItOut throws an instance of TestExc, the first (innermost) applicable exception handler in the exception table is chosen to handle the exception. The code for that exception handler, beginning at index *7*, passes the thrown value to handleExc and on its return makes the same subroutine call to the finally block at index *26* as in the normal case. If an exception is not thrown by handleExc, tryCatchFinally returns normally. 

If tryItOut throws a value that is not an instance of TestExc or if handleExc itself throws an exception, the condition is handled by the second entry in the exception table, which handles any value thrown between indices *0* and *16*. That exception handler transfers control to index *20*, where the thrown value is first stored in local variable *1*. The code for the finally block at index *26* is called as a subroutine. If it returns, the thrown value is retrieved from local variable *1* and rethrown using the *athrow* instruction. If a new value is thrown during execution of the finally clause, the finally clause aborts, and tryCatchFinally returns abruptly, throwing the new value to its invoker. 