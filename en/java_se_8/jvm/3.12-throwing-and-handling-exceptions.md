#### 3.12 **Throwing and Handling Exceptions** 

Exceptions are thrown from programs using the throw keyword. Its compilation is simple: 

```java
void cantBeZero(int i) throws TestExc {
  if (i == 0) {
  	throw new TestExc();
  }
}
```

becomes: 

```
Method void cantBeZero(int)
0 iload_1							// Push argument 1 (i)
1 ifne 12							// If i==0, allocate instance and throw 
4 new #1							// Create instance of TestExc
7 dup									// One reference goes to its constructor
8 invokespecial #7		// Method TestExc.<init>()V
11 athrow 						// Second reference is thrown
12 return							// Never get here if we threw TestExc
```

Compilation of try-catch constructs is straightforward. For example: 

```java
void catchOne() {
  try {
  	tryItOut();
  } catch (TestExc e) {
  	handleExc(e);
  }
}
```

is compiled as: 

```
Method void catchOne()
0 aload_0								// Beginning of try block
1 invokevirtual #6			// Method Example.tryItOut()V
4 return								// End of try block; normal return
5 astore_1							// Store thrown value in local var 1 
6 aload_0								// Push this
7 aload_1								// Push thrown value
8 invokevirtual #5			// Invoke handler method:
												// Example.handleExc(LTestExc;)V
11 return								// Return after handling TestExc

Exception table:				
From		To		Target		Type
0				4 		5					Class TestExc
```

Looking more closely, the try block is compiled just as it would be if the try were not present: 

```
Method void catchOne()
0 aload_0							// Beginning of try block
1 invokevirtual #6		// Method Example.tryItOut()V
4 return							// End of try block; normal return
```

If no exception is thrown during the execution of the try block, it behaves as though the try were not there: tryItOut is invoked and catchOne returns. 

Following the try block is the Java Virtual Machine code that implements the single catch clause: 

```
5 astore_1						// Store thrown value in local var 1
6 aload_0							// Push this
7 aload_1							// Push thrown value
8 invokevirtual #5		// Invoke handler method:
											// Example.handleExc(LTestExc;)V
11 return							// Return after handling TestExc

Exception table:	
From		To		Target		Type
0				4 		5					Class TestExc
```

The invocation of handleExc, the contents of the catch clause, is also compiled like a normal method invocation. However, the presence of a catch clause causes the compiler to generate an exception table entry (ยง2.10, ยง4.7.3). The exception table for the catchOne method has one entry corresponding to the one argument (an instance of class TestExc) that the catch clause of catchOne can handle. If some value that is an instance of TestExc is thrown during execution of the instructions between indices *0* and *4* in catchOne, control is transferred to the Java Virtual Machine code at index *5*, which implements the block of the catch clause. If the value that is thrown is not an instance of TestExc, the catch clause of catchOne cannot handle it. Instead, the value is rethrown to the invoker of catchOne. 

A try may have multiple catch clauses: 

```java
void catchTwo() {
  try {
  	tryItOut();
  } catch (TestExc1 e) {
  	handleExc(e);
  } catch (TestExc2 e) {
  	handleExc(e);
  }
}
```

Multiple catch clauses of a given try statement are compiled by simply appending the Java Virtual Machine code for each catch clause one after the other and adding entries to the exception table, as shown: 

```
Method void catchTwo()
0 aload_0									// Begin try block
1 invokevirtual #5				// Method Example.tryItOut()V
4 return									// End of try block; normal return
5 astore_1								// Beginning of handler for TestExc1;
													// Store thrown value in local var 1
6 aload_0									// Push this
7 aload_1									// Push thrown value
8 invokevirtual #7				// Invoke handler method:
													// Example.handleExc(LTestExc1;)V
11 return									// Return after handling TestExc1
12 astore_1								// Beginning of handler for TestExc2;
													// Store thrown value in local var 1
13 aload_0								// Push this
14 aload_1								// Push thrown value
15 invokevirtual #7				// Invoke handler method:
													// Example.handleExc(LTestExc2;)V
18 return									// Return after handling TestExc2
   
Exception table:				
From		To		Target		Type
0				4 		5					Class TestExc1
0 			4 		12				Class TestExc2
```

If during the execution of the try clause (between indices *0* and *4*) a value is thrown that matches the parameter of one or more of the catch clauses (the value is an instance of one or more of the parameters), the first (innermost) such catch clause is selected. Control is transferred to the Java Virtual Machine code for the block of that catch clause. If the value thrown does not match the parameter of any of the catch clauses of catchTwo, the Java Virtual Machine rethrows the value without invoking code in any catch clause of catchTwo. 

Nested try-catch statements are compiled very much like a try statement with multiple catch clauses: 

```java
void nestedCatch() {
  try {
    try {
    	tryItOut();
    } catch (TestExc1 e) {
    	handleExc1(e);
    }
  } catch (TestExc2 e) {
		handleExc2(e);
  }
}
```

becomes: 

```java
Method void nestedCatch()
0 aload_0								// Begin try block
1 invokevirtual #8			// Method Example.tryItOut()V
4 return								// End of try block; normal return
5 astore_1							// Beginning of handler for TestExc1;
												// Store thrown value in local var 1
6 aload_0								// Push this
7 aload_1								// Push thrown value
8 invokevirtual #7			// Invoke handler method:
												// Example.handleExc1(LTestExc1;)V
11 return								// Return after handling TestExc1
12 astore_1							// Beginning of handler for TestExc2;
												// Store thrown value in local var 1
13 aload_0							// Push this
14 aload_1							// Push thrown value
15 invokevirtual #6			// Invoke handler method:
												// Example.handleExc2(LTestExc2;)V
18 return								// Return after handling TestExc2

Exception table:	
From		To		Target		Type
0				4 		5					Class TestExc1
0 			4 		12				Class TestExc2
```

The nesting of catch clauses is represented only in the exception table. The Java Virtual Machine does not enforce nesting of or any ordering of the exception table entries (ยง2.10). However, because try-catch constructs are structured, a compiler can always order the entries of the exception handler table such that, for any thrown exception and any program counter value in that method, the first exception handler that matches the thrown exception corresponds to the innermost matching catch clause. 

For instance, if the invocation of tryItOut (at index *1*) threw an instance of TestExc1, it would be handled by the catch clause that invokes handleExc1. This is so even though the exception occurs within the bounds of the outer catch clause (catching TestExc2) and even though that outer catch clause might otherwise have been able to handle the thrown value. 

As a subtle point, note that the range of a catch clause is inclusive on the "from" end and exclusive on the "to" end (ยง4.7.3). Thus, the exception table entry for the catch clause catching TestExc1 does not cover the *return* instruction at offset *4*. However, the exception table entry for the catch clause catching TestExc2 does cover the *return* instruction at offset *11*. Return instructions within nested catch clauses are included in the range of instructions covered by nesting catch clauses. 