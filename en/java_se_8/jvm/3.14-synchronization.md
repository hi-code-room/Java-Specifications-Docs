#### 3.14 **Synchronization** 

Synchronization in the Java Virtual Machine is implemented by monitor entry and exit, either explicitly (by use of the *monitorenter* and *monitorexit* instructions) or implicitly (by the method invocation and return instructions). 

For code written in the Java programming language, perhaps the most common form of synchronization is the synchronized method. A synchronized method is not normally implemented using *monitorenter* and *monitorexit*. Rather, it is simply distinguished in the run-time constant pool by the ACC_SYNCHRONIZED flag, which is checked by the method invocation instructions (ยง2.11.10). 

The *monitorenter* and *monitorexit* instructions enable the compilation of synchronized statements. For example: 

```java
void onlyMe(Foo f) {
  synchronized(f) {
  	doSomething();
  }
}
```

is compiled to: 

```java
Method void onlyMe(Foo)
0 aload_1						// Push f
1 dup								// Duplicate it on the stack
2 astore_2					// Store duplicate in local variable 2
3 monitorenter			// Enter the monitor associated with f
4 aload_0						// Holding the monitor, pass this and...
5 invokevirtual #5	// ...call Example.doSomething()V
8 aload_2						// Push local variable 2 (f)
9 monitorexit				// Exit the monitor associated with f 
10 goto 18					// Complete the method normally
13 astore_3					// In case of any throw, end up here
14 aload_2					// Push local variable 2 (f)
15 monitorexit			// Be sure to exit the monitor!
16 aload_3					// Push thrown value...
17 athrow						// ...and rethrow value to the invoker
18 return						// Return in the normal case
   
Exception table:	
From		To		Target		Type
4 			10 		13 				any 
13 			16 		13 				any
```

The compiler ensures that at any method invocation completion, a *monitorexit* instruction will have been executed for each *monitorenter* instruction executed since the method invocation. This is the case whether the method invocation completes normally (ยง2.6.4) or abruptly (ยง2.6.5). To enforce proper pairing of *monitorenter* and *monitorexit* instructions on abrupt method invocation completion, the compiler generates exception handlers (ยง2.10) that will match any exception and whose associated code executes the necessary *monitorexit* instructions. 