#### 3.2 **Use of Constants, Local Variables, and Control Constructs** 

Java Virtual Machine code exhibits a set of general characteristics imposed by the Java Virtual Machine's design and use of types. In the first example we encounter many of these, and we consider them in some detail. 

The spin method simply spins around an empty for loop 100 times: 

```java
void spin() {
  int i;
  for (i = 0; i < 100; i++) {
    ;    // Loop body is empty
  }
}
```

A compiler might compile spin to: 

```shell
0 iconst_0 			// Push int constant 0
1 istore_1 			// Store into local variable 1 (i=0)
2 goto 8 				// First time through don't increment
5 iinc 1 1 			// Increment local variable 1 by 1 (i++)
8 iload_1 			// Push local variable 1 (i)
9 bipush 100 		// Push int constant 100
11 if_icmplt 5 	// Compare and loop if less than (i < 100)
14 return 			// Return void when done
```

The Java Virtual Machine is stack-oriented, with most operations taking one or more operands from the operand stack of the Java Virtual Machine's current frame or pushing results back onto the operand stack. A new frame is created each time a method is invoked, and with it is created a new operand stack and set of local variables for use by that method (ยง2.6). At any one point of the computation, there are thus likely to be many frames and equally many operand stacks per thread of control, corresponding to many nested method invocations. Only the operand stack in the current frame is active. 

The instruction set of the Java Virtual Machine distinguishes operand types by using distinct bytecodes for operations on its various data types. The method spin operates only on values of type int. The instructions in its compiled code chosen to operate on typed data (*iconst_0*, *istore_1*, *iinc*, *iload_1*, *if_icmplt*) are all specialized for type int. 

The two constants in spin, 0 and 100, are pushed onto the operand stack using two different instructions. The 0 is pushed using an *iconst_0* instruction, one of the family of *iconst_* instructions. The 100 is pushed using a *bipush* instruction, which fetches the value it pushes as an immediate operand. 

The Java Virtual Machine frequently takes advantage of the likelihood of certain operands (int constants *-1*, *0*, *1*, *2*, *3*, *4* and *5* in the case of the *iconst_* instructions) by making those operands implicit in the opcode. Because the *iconst_0* instruction knows it is going to push an int 0, *iconst_0* does not need to store an operand to tell it what value to push, nor does it need to fetch or decode an operand. Compiling the push of 0 as *bipush 0* would have been correct, but would have made the compiled code for spin one byte longer. A simple virtual machine would have also spent additional time fetching and decoding the explicit operand each time around the loop. Use of implicit operands makes compiled code more compact and efficient. 

The int i in spin is stored as Java Virtual Machine local variable *1*. Because most Java Virtual Machine instructions operate on values popped from the operand stack rather than directly on local variables, instructions that transfer values between local variables and the operand stack are common in code compiled for the Java Virtual Machine. These operations also have special support in the instruction set. In spin, values are transferred to and from local variables using the *istore_1* and *iload_1* instructions, each of which implicitly operates on local variable *1*. The *istore_1* instruction pops an int from the operand stack and stores it in local variable *1*. The *iload_1* instruction pushes the value in local variable *1* on to the operand stack. 

The use (and reuse) of local variables is the responsibility of the compiler writer. The specialized load and store instructions should encourage the compiler writer to reuse local variables as much as is feasible. The resulting code is faster, more compact, and uses less space in the frame. 

Certain very frequent operations on local variables are catered to specially by the Java Virtual Machine. The *iinc* instruction increments the contents of a local variable by a one-byte signed value. The *iinc* instruction in spin increments the first local variable (its first operand) by *1* (its second operand). The *iinc* instruction is very handy when implementing looping constructs. 

The for loop of spin is accomplished mainly by these instructions: 

```
5 iinc 1 1 // Increment local variable 1 by 1 (i++)
8 iload_1 // Push local variable 1 (i)
9 bipush 100 // Push int constant 100
11 if_icmplt 5 // Compare and loop if less than (i < 100)
```

The *bipush* instruction pushes the value *100* onto the operand stack as an int, then the *if_icmplt* instruction pops that value off the operand stack and compares it against *i*. If the comparison succeeds (the variable i is less than 100), control is transferred to index *5* and the next iteration of the for loop begins. Otherwise, control passes to the instruction following the *if_icmplt*. 

If the spin example had used a data type other than int for the loop counter, the compiled code would necessarily change to reflect the different data type. For instance, if instead of an int the spin example uses a double, as shown: 

```java
void dspin() {
	double i;
	for (i = 0.0; i < 100.0; i++) { 
		; // Loop body is empty
	} 
}
```

the compiled code is: 

```
Method void dspin()
0 dconst_0 		// Push double constant 0.0
1 dstore_1 		// Store into local variables 1 and 2
2 goto 9 			// First time through don't increment
5 dload_1 		// Push local variables 1 and 2
6 dconst_1 		// Push double constant 1.0
7 dadd 				// Add; there is no dinc instruction
8 dstore_1		// Store result in local variables 1 and 2
9 dload_1 		// Push local variables 1 and 2
10 ldc2_w #4 	// Push double constant 100.0
13 dcmpg 			// There is no if_dcmplt instruction
14 iflt 5 		// Compare and loop if less than (i < 100.0)
17 return 		// Return void when done
```

The instructions that operate on typed data are now specialized for type double. (The *ldc2_w* instruction will be discussed later in this chapter.) 

Recall that double values occupy two local variables, although they are only accessed using the lesser index of the two local variables. This is also the case for values of type long. Again for example, 

```java
double doubleLocals(double d1, double d2) {
       return d1 + d2;
}
```

becomes 

```
Method double doubleLocals(double,double)
0 dload_1 // First argument in local variables 1 and 2
1 dload_3 // Second argument in local variables 3 and 4
2 dadd
3 dreturn
```

Note that local variables of the local variable pairs used to store double values in doubleLocals must never be manipulated individually. 

The Java Virtual Machine's opcode size of 1 byte results in its compiled code being very compact. However, 1-byte opcodes also mean that the Java Virtual Machine instruction set must stay small. As a compromise, the Java Virtual Machine does not provide equal support for all data types: it is not completely orthogonal (Table 2.11.1-A). 

For example, the comparison of values of type int in the for statement of example spin can be implemented using a single *if_icmplt* instruction; however, there is no single instruction in the Java Virtual Machine instruction set that performs a conditional branch on values of type double. Thus, dspin must implement its comparison of values of type double using a *dcmpg* instruction followed by an *iflt* instruction. 

The Java Virtual Machine provides the most direct support for data of type int. This is partly in anticipation of efficient implementations of the Java Virtual Machine's operand stacks and local variable arrays. It is also motivated by the frequency of int data in typical programs. Other integral types have less direct support. There are no byte, char, or short versions of the store, load, or add instructions, for instance. Here is the spin example written using a short: 

```java
void sspin() {
  short i;
  for (i = 0; i < 100; i++) {
		;    // Loop body is empty
	}
}
```

It must be compiled for the Java Virtual Machine, as follows, using instructions operating on another type, most likely int, converting between short and int values as necessary to ensure that the results of operations on short data stay within the appropriate range: 

```
Method void sspin()
0 iconst_0
1 istore_1
2 goto 10
5 iload_1 	// The short is treated as though an int
6 iconst_1
7 iadd
8 i2s       // Truncate int to short
9 istore_1
10 iload_1
11 bipush 100 
13 if_icmplt 5 
16 return
```

The lack of direct support for byte, char, and short types in the Java Virtual Machine is not particularly painful, because values of those types are internally promoted to int (byte and short are sign-extended to int, char is zero-extended). Operations on byte, char, and short data can thus be done using int instructions. The only additional cost is that of truncating the values of int operations to valid ranges. 

The long and floating-point types have an intermediate level of support in the Java Virtual Machine, lacking only the full complement of conditional control transfer instructions. 