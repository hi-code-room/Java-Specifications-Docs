#### 3.8 **Working with Class Instances** 

Java Virtual Machine class instances are created using the Java Virtual Machine's *new* instruction. Recall that at the level of the Java Virtual Machine, a constructor appears as a method with the compiler-supplied name <init>. This specially named method is known as the instance initialization method (ยง2.9). Multiple instance initialization methods, corresponding to multiple constructors, may exist for a given class. Once the class instance has been created and its instance variables, including those of the class and all of its superclasses, have been initialized to their default values, an instance initialization method of the new class instance is invoked. For example: 

```java
Object create() {
	return new Object();
}
```

compiles to: 

```
Method java.lang.Object create()
0 new #1 						// Class java.lang.Object
3 dup
4 invokespecial #4 	// Method java.lang.Object.<init>()V 
7 areturn
```

Class instances are passed and returned (as reference types) very much like numeric values, although type reference has its own complement of instructions, for example: 

```java
	int i;
  MyObj example() {
  	MyObj o = new MyObj();
  	return silly(o);
  }
  
  MyObj silly(MyObj o) {
  	if (o != null) {
  		return o;
  	} else {
			return o; 
		}
	}
```

becomes: 

```
Method MyObj example() 
0 new #2							// Class MyObj
3 dup
4 invokespecial #5		// Method MyObj.<init>()V
7 astore_1
8 aload_0
9 aload_1
10 invokevirtual #4		// Method Example.silly(LMyObj;)LMyObj;
13 areturn
```

```
Method MyObj silly(MyObj)
0 aload_1
1 ifnull 6
4 aload_1
5 areturn 6 aload_1 
7 areturn
```

The fields of a class instance (instance variables) are accessed using the *getfield* and *putfield* instructions. If i is an instance variable of type int, the methods setIt and getIt, defined as: 

```java
void setIt(int value) {
	i = value;
}

int getIt() {
	return i; 
}
```

become: 

```
Method void setIt(int)
0 aload_0
1 iload_1
2 putfield #4		// Field Example.i I
5 return

Method int getIt()
0 aload_0
1 getfield #4		// Field Example.i I
4 ireturn
```

As with the operands of method invocation instructions, the operands of the *putfield* and *getfield* instructions (the run-time constant pool index *#4*) are not the offsets of the fields in the class instance. The compiler generates symbolic references to the fields of an instance, which are stored in the run-time constant pool. Those run- time constant pool items are resolved at run-time to determine the location of the field within the referenced object. 