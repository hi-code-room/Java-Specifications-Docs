#### 3.5 **More Control Examples** 

Compilation of for statements was shown in an earlier section (§3.2). Most of the Java programming language's other control constructs (if-then-else, do, while, break, and continue) are also compiled in the obvious ways. The compilation of switch statements is handled in a separate section (§3.10), as are the compilation of exceptions (§3.12) and the compilation of finally clauses (§3.13). 

As a further example, a while loop is compiled in an obvious way, although the specific control transfer instructions made available by the Java Virtual Machine vary by data type. As usual, there is more support for data of type int, for example: 

```java
void whileInt() {
  int i = 0;
  while (i < 100) {
		i++;
	} 
}
```

is compiled to: 

```
Method void whileInt()
0 iconst_0
1 istore_1
2 goto 8
5 iinc 1 1
8 iload_1
9 bipush 100
11 if_icmplt 5 
14 return
```

Note that the test of the while statement (implemented using the *if_icmplt* instruction) is at the bottom of the Java Virtual Machine code for the loop. (This was also the case in the spin examples earlier.) The test being at the bottom of the loop forces the use of a *goto* instruction to get to the test prior to the first iteration of the loop. If that test fails, and the loop body is never entered, this extra instruction is wasted. However, while loops are typically used when their body is expected to be run, often for many iterations. For subsequent iterations, putting the test at the bottom of the loop saves a Java Virtual Machine instruction each time around the loop: if the test were at the top of the loop, the loop body would need a trailing *goto* instruction to get back to the top. 

Control constructs involving other data types are compiled in similar ways, but must use the instructions available for those data types. This leads to somewhat less efficient code because more Java Virtual Machine instructions are needed, for example: 

```java
void whileDouble() { 
	double i = 0.0; 
	while (i < 100.1) { 
		i++; 
	} 
} 
```

is compiled to: 

```
Method void whileDouble() 0 dconst_0
1 dstore_1
2 goto 9
5 dload_1 
6 dconst_1 
7 dadd
8 dstore_1 
9 dload_1 
10 ldc2_w #4 	// Push double constant 100.1
13 dcmpg			// To compare and branch we have to use... 
14 iflt 5 		// ...two instructions
17 return
```

Each floating-point type has two comparison instructions: *fcmpl* and *fcmpg* for type float, and *dcmpl* and *dcmpg* for type double. The variants differ only in their treatment of NaN. NaN is unordered (§2.3.2), so all floating-point comparisons fail if either of their operands is NaN. The compiler chooses the variant of the comparison instruction for the appropriate type that produces the same result whether the comparison fails on non-NaN values or encounters a NaN. For instance: 

```java
int lessThan100(double d) { 
	if (d < 100.0) {
		return 1;
	} else {
		return -1; 
	}
}
```

compiles to: 

```
Method int lessThan100(double) 
0 dload_1
1 ldc2_w #4
4 dcmpg				// Push double constant 100.0
5 ifge 10			// Push 1 if d is NaN or d > 100.0;
							// push 0 if d == 100.0
8 iconst_1 		// Branch on 0 or 1
9 ireturn 
10 iconst_m1 
11 ireturn
```

If d is not NaN and is less than 100.0, the *dcmpg* instruction pushes an int *-1* onto the operand stack, and the *ifge* instruction does not branch. Whether d is greater than 100.0 or is NaN, the *dcmpg* instruction pushes an int *1* onto the operand stack, and the *ifge* branches. If d is equal to 100.0, the *dcmpg* instruction pushes an int *0* onto the operand stack, and the *ifge* branches. 

The *dcmpl* instruction achieves the same effect if the comparison is reversed: 

```java
int greaterThan100(double d) { 
	if (d > 100.0) {
		return 1;
	} else {
		return -1; 
  }
}
```

becomes: 

```
Method int greaterThan100(double) 
0 dload_1
1 ldc2_w #4			// Push double constant 100.0
4 dcmpl					// Push -1 if d is NaN or d < 100.0;
								// push 0 if d == 100.0
5 ifle 10				// Branch on 0 or -1
8 iconst_1 
9 ireturn 
10 iconst_m1 
11 ireturn
```

Once again, whether the comparison fails on a non-NaN value or because it is passed a NaN, the *dcmpl* instruction pushes an int value onto the operand stack that causes the *ifle* to branch. If both of the *dcmp* instructions did not exist, one of the example methods would have had to do more work to detect NaN. 