#### 3.9 **Arrays** 

Java Virtual Machine arrays are also objects. Arrays are created and manipulated using a distinct set of instructions. The *newarray* instruction is used to create an array of a numeric type. The code: 

```java
void createBuffer() {
  int buffer[];
  int bufsz = 100;
  int value = 12;
  buffer = new int[bufsz];
  buffer[10] = value;
  value = buffer[11];
}
```

might be compiled to: 

```
Method void createBuffer()
0 bipush 100		// Push int constant 100 (bufsz)
2 istore_2			// Store bufsz in local variable 2
3 bipush 12			// Push int constant 12 (value)
5 istore_3			// Store value in local variable 3
6 iload_2				// Push bufsz...
7 newarray int	// ...and create new int array of that length
9 astore_1			// Store new array in buffer
10 aload_1			// Push buffer
11 bipush 10 		// Push int constant 10
13 iload_3 			// Push value
14 iastore 			// Store value at buffer[10]
15 aload_1 			// Push buffer
16 bipush 11		// Push int constant 11
18 iaload				// Push value at buffer[11]...
19 istore_3			// ...and store it in value
20 return
```

The *anewarray* instruction is used to create a one-dimensional array of object references, for example: 

```java
void createThreadArray() {
  Thread threads[];
  int count = 10;
  threads = new Thread[count];
  threads[0] = new Thread();
}
```

becomes: 

```
Method void createThreadArray()
0 bipush 10						// Push int constant 10
2 istore_2						// Initialize count to that
3 iload_2							// Push count, used by anewarray
4 anewarray class #1 	// Create new array of class Thread
7 astore_1						// Store new array in threads
8 aload_1							// Push value of threads
9 iconst_0						// Push int constant 0
10 new #1							// Create instance of class Thread
13 dup								// Make duplicate reference...
14 invokespecial #5		// ...for Thread's constructor
											// Method java.lang.Thread.<init>()V 
17 aastore						// Store new Thread in array at 0
18 return							
```

The *anewarray* instruction can also be used to create the first dimension of a multidimensional array. Alternatively, the *multianewarray* instruction can be used to create several dimensions at once. For example, the three-dimensional array: 

```java
int[][][] create3DArray() {
  int grid[][][];
  grid = new int[10][5][];
  return grid;
}
```

is created by: 

```
Method int create3DArray()[][][]
0 bipush 10									// Push int 10 (dimension one)
2 iconst_5									// Push int 5 (dimension two)
3 multianewarray #1 dim #2	// Class [[[I, a three-dimensional
														// int array; only create the
														// first two dimensions
7 astore_1									// Store new array...
8 aload_1										// ...then prepare to return it
9 areturn
```

The first operand of the *multianewarray* instruction is the run-time constant pool index to the array class type to be created. The second is the number of dimensions of that array type to actually create. The *multianewarray* instruction can be used to create all the dimensions of the type, as the code for create3DArray shows. Note that the multidimensional array is just an object and so is loaded and returned by an *aload_1* and *areturn* instruction, respectively. For information about array class names, see ยง4.4.1. 

All arrays have associated lengths, which are accessed via the *arraylength* instruction. 